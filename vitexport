#!/bin/tcsh
#
# Pushes tabled vital files, made by VIT2TBL.M, or indeed its
# runtime-compiled (MCC) version VIT2TBL, to the GEOWEB server
#
# Last input from aetimmes-at-princeton.edu, 10/08/2018
# Last modified by fjsimons-at-alum.mit.edu, 05/19/2022

# Where are these being kept?
set fromwhere = /u/fjsimons/MERMAID/serverdata/tbldata

# Where are these being sent to?
set tothere = /u/fjsimons/MERMAID/serverdata/locdata

# Explicit list of serial numbers:
# -------------------------------------------------------------------------
# owned by GeoAzur    452.020-P-??    (6-7)
# owned by Princeton  452.020-P-??    (8-13, 16-25)
# owned by Kobe Uni   452.112-N-??    (1-5)
# owned by SUSTC      452.020-P-????  (26-49)
# owned by Kobe Uni   452.020-P-????  (50, 52-54)
# owned by SUSTC      452.120-R-????  (58-73)
# owned by OSEAN      452.210-P-????  (14) 
# owned by OSEAN      452.020-P-????  (51, 56-57, 64, 84)
# owned by OSEAN      452.120-R-????  (0, 80, 84)
# owned by Stanford   465.152-R-????  (1-7)
# owned by SUSTECH    465.152-R-????  (58-59, 61, 63, 65, 67, 69, 71-73)
# -------------------------------------------------------------------------
# no sign of a any other numbers (15, ...) and 84 is double...
# 
# Died undeployed: P-30 since 2019-08-07 SUSTEC
# Died in service: P-07 since 2018-09-11 Geoazur
# Died in service: N-03 since 2019-01-02 JAMSTEC
# Died in service: P-34 since 2019-11-16 SUSTC
# Died in service: P-47 since 2020-03-28 SUSTC
# Died in service: R-06 since 2021-10-08 STANFORD
# Died exhausted:     50 from JAMSTEC  (tsunami)
#
# So... KOBE \ GEOAZUR \ PRINCETON \ SUSTECH \ KOBE
#   ... STANFORD \ SUSTECH
#
# Remember NOTHING can follow the continuation symbol on the same line...
set lettrs = ( N  N  N  N  N \
               P  P \
               P  P  P  P  P  P     P  P  P  P  P  P  P  P  P P \
               P  P  P  P     P  P  P  P  P  P  P  P  P  P  P  P  P  P  P  P  P  P  P \
               P     P  P  P \
               R  R  R  R  R  R  R \
               R  R     R     R    R    R    R      R   R   R)
# Remember we're trying to capture the two- and four-digits conventions in one loop
set floats = (01 02 03 04 05 \
              06 07 \
              08 09 10 11 12 13    16 17 18 19 20 21 22 23 24 25 \
              26 27 28 29    31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49 \
              50    52 53 54 \
              01 02 03 04 05 06 07 \
              58 59    61    63    65    67    69    71 72 73)

# Remove the ALL file before making it again
/bin/rm -f $tothere/all.txt

# Do them in the loop - watch the treatment of leading zeroes and letters
@ index = 0
foreach float ($floats[*])
     @ index += 1
     set letter = $lettrs[$index]
     # Individual floats, last 2*30 lines only
     tail -60    $fromwhere/???.???-$letter-*$float.tbl  >! $tothere/{$letter}00{$float}_030.txt
     # Individual floats, complete history
     /bin/cp -f  $fromwhere/???.???-$letter-*$float.tbl     $tothere/{$letter}00${float}_all.txt
     # Collective files, only their last line
     tail -1     $fromwhere/???.???-$letter-*$float.tbl >>! $tothere/all.txt
     # Collective files, only their first line, need to do this only once obviously...
     # head -1 -q  $fromwhere/???.???-$letter-*$float.tbl >>! $tothere/misalo.txt
end

# Then all of those get pushed on to the server via a passwordless
# single-command-use SSH key for back up to a directory that is
# specified via the remote public key by prepending the relevant
# information in the authorized_keys file (for us here it is
# /home/www/people/simons/SOM on geoweb.princeton.edu), so that the
# pushing can be done via CRON without passwords being prompted
# But watch, as in PTON2MARK, to not earlier set any ssh-add key preceding with overriding preference
set finaldest = /home/www/people/simons/SOM
rsync -ve "ssh -i /u/fjsimons/.ssh/id_remote_backup" $tothere/*_030.txt  fjsimons@geoweb.princeton.edu:$finaldest
rsync -ve "ssh -i /u/fjsimons/.ssh/id_remote_backup" $tothere/*_all.txt  fjsimons@geoweb.princeton.edu:$finaldest
rsync -ve "ssh -i /u/fjsimons/.ssh/id_remote_backup" $tothere/all.txt    fjsimons@geoweb.princeton.edu:$finaldest
rsync -ve "ssh -i /u/fjsimons/.ssh/id_remote_backup" $tothere/misalo.txt fjsimons@geoweb.princeton.edu:$finaldest
